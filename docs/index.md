---
layout: ""
page_title: "Provider: BitLaunch"
description: |-
  The BitLaunch provider provides resources to create and manage BitLaunch.io VMs.
---

# BitLaunch Provider

The BitLaunch provider provides resources to create and manage [BitLaunch.io](https://bitlaunch.io/) VMs,
, which can be paid for using Bitcoin, Etherium, or Litecoin.

If you find this project useful, feel free to buy me a coffee in BTC at `16g88jxnX315CnjTDbfZ9hwuWgeSbRJdMG`.

For more information also see the [BitLaunch API Docs](https://developers.bitlaunch.io/reference),
as a most of the basic API objects have a 1:1 mapping to Terraform Resources or Data sources.

## Example Usage

```terraform
terraform {
  required_providers {
    bitlaunch = {
      version = "~> 0.0.1"
      source  = "hashicorp.com/pathtofile/bitlaunch"
    }
  }
}

variable "token" { sensitive = true }
variable "ssh_pubkey" { type = string }
variable "host" { default = "DigitalOcean" }

provider "bitlaunch" {
  token = var.token
}

// Data
data "bitlaunch_image" "image" {
  host         = var.host
  distro_name  = "Ubuntu"
  version_name = "20.04 (LTS) x64"
}

data "bitlaunch_region" "region" {
  host        = var.host
  region_name = "San Francisco"
  slug        = "sfo2"
}

data "bitlaunch_size" "size" {
  host      = var.host
  cpu_count = 1
  memory_mb = 1024
}

// Resources
resource "bitlaunch_sshkey" "sshkey" {
  name    = "tf_sshkeys"
  content = var.ssh_pubkey
}

resource "bitlaunch_server" "server" {
  host        = var.host
  name        = "tf_server"
  image_id    = data.bitlaunch_image.image.id
  size_id     = data.bitlaunch_size.size.id
  region_id   = data.bitlaunch_region.region.id
  ssh_keys    = [bitlaunch_sshkey.sshkey.id]
  wait_for_ip = true
}

// Outputs
output "ip_address" {
  value = bitlaunch_server.server.ipv4
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `token` (String, Sensitive) API Token
